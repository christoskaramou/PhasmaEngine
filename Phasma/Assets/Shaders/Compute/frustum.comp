/*
Copyright (c) 2018-2021 Christos Karamoustos

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#version 450

struct Plane
{
	vec3 normal;
	float d;
};

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout(std430, binding = 0) readonly buffer DataIn { mat4 viewProjection; } dataIn;
layout(std430, binding = 1) writeonly buffer DataOut { Plane frustum[6]; } dataOut;


void main()
{
	// transpose just to make the calculations look simpler
	mat4 pvm = transpose(dataIn.viewProjection);

	/* Extract the numbers for the RIGHT plane */
	vec4 temp = pvm[3] - pvm[0];
	temp /= length(temp.xyz);

	dataOut.frustum[0].normal = temp.xyz;
	dataOut.frustum[0].d = temp.w;

	/* Extract the numbers for the LEFT plane */
	temp = pvm[3] + pvm[0];
	temp /= length(temp.xyz);

	dataOut.frustum[1].normal = temp.xyz;
	dataOut.frustum[1].d = temp.w;

	/* Extract the BOTTOM plane */
	temp = pvm[3] - pvm[1];
	temp /= length(temp.xyz);

	dataOut.frustum[2].normal = temp.xyz;
	dataOut.frustum[2].d = temp.w;

	/* Extract the TOP plane */
	temp = pvm[3] + pvm[1];
	temp /= length(temp.xyz);

	dataOut.frustum[3].normal = temp.xyz;
	dataOut.frustum[3].d = temp.w;

	/* Extract the FAR plane */
	temp = pvm[3] - pvm[2];
	temp /= length(temp.xyz);

	dataOut.frustum[4].normal = temp.xyz;
	dataOut.frustum[4].d = temp.w;

	/* Extract the NEAR plane */
	temp = pvm[3] + pvm[2];
	temp /= length(temp.xyz);

	dataOut.frustum[5].normal = temp.xyz;
	dataOut.frustum[5].d = temp.w;
}